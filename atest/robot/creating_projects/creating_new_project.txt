*** Settings ***
Suite Setup     Initialize Test Data
Suite Teardown  Remove Test File
Test Setup      Start RIDE
Resource        atest_resources.txt

*** Variables ***
${test project}  test_project
${test project name}  Test Project
${test filename}  test_project.txt

*** Test Cases ***
New Project Dont Save
    Create New Project  ${test project}
    Set Documentation  This is My Documentation
    Create New Test Case  ${test project}  My Test
    Close RIDE and Dont Save

New Project And Test Case Save
    Create New Project  ${test project}
    Set Documentation  This is My Documentation
    Create New Test Case  ${test project}  My Test
    Add Data To Row  1  Log  My  Cells
    Select Tree Element  *${test project name}
    Window Does Not Have Text  My Test
    Select Tree Element  *${test project name}|My Test
    Window Has Text  My Test
    Close RIDE and Save

Open Previous Project
    Open  ${test filename}
    Window Has Text  ${test project name}
    Window Does Not Have Text  My Test
    Select Tree Element  ${test project name}|My Test
    Window Has Text  My Test
    Close Ride

*** Keywords ***
Remove Test File
    ${filename} =  Join Path  ${TEMP DIR}  ${test filename}
    Remove File  ${filename}

Create New Project
    [Arguments]  ${name}
    Send  ^n
    Wait For Active Window  New Project
    Send  ${name}
    Control Set Text  New Project  ${EMPTY}  Edit2  ${TESTDATA PATH}
    Control Click  New Project  ${EMPTY}  Button11
    Wait For Active Window  RIDE  Suite Setup

