*** Settings ***
Suite Setup     Close Ride
Suite Teardown  Run Keywords  Remove Test File  Start Ride
Test Setup      Start RIDE
Resource        atest_resources.txt


*** Variables ***
${test project}  test_project
${test project name}  Test Project
${test filename}  test_project.html

*** Test Cases ***
New Project Dont Save
    Create New Project  ${test project}
    Create Documentation  This is My Documentation
    Create New Test Case  ${test project}  My Test
    Close RIDE and Dont Save

New Project And Test Case Save
    Create New Project  ${test project}
    Create Documentation  This is My Documentation
    Create New Test Case  ${test project}  My Test
    Add Data To Row  1  Log  My  Cells
    Select Tree Element  *${test project name}
    Window Does Not Have Text  My Test
    Select Tree Element  *${test project name}|My Test
    Window Has Text  My Test
    Close RIDE and Save

Open Previous Project
    Open Project  ${test filename}
    Window Has Text  ${test project name}
    Window Does Not Have Text  My Test
    Select Tree Element  ${test project name}|My Test
    Window Has Text  My Test
    Close Ride


*** Keywords ***
Remove Test File
    ${filename} =  Join Path  ${TEMP DIR}  ${test filename}
    Remove File  ${filename}

Create New Project
    [Arguments]  ${name}
    Send  ^n
    Wait For Active Window  New Project
    Send  ${name}
    Control Set Text  New Project  ${EMPTY}  Edit2  ${TEMP DIR}
    Control Click  New Project  ${EMPTY}  Button11
    Wait For Active Window  RIDE  Suite Setup

Open Project
    [Arguments]  ${name}
    Send  ^o
    Wait For Active Window  Open
    ${filename} =  Join Path  ${TEMP DIR}  ${name}
    Control Set Text  Open  ${EMPTY}  Edit1  ${filename}
    Control Click  Open  ${EMPTY}  Button2
    Wait For Active Window  RIDE  Suite Setup

Create Documentation
    [Arguments]  ${text}
    Control Click  RIDE  ${EMPTY}  Button1
    Wait For Active Window  Documentation
    Send  ${text}
    Control Click  Documentation  ${EMPTY}  Button1