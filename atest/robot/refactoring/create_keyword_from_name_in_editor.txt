*** Settings ***
Suite Setup     Open  refactoring\\create_keyword.txt
Test Teardown   Save
Resource        atest_resources.txt

*** Test Cases ***
Create New Keyword
    Given Grid Contains Text
    When User Selects Create New UK from Context Menu on Selection
    Then UK with Selected Text as Name Is Created
    And History Back Points to the Initial Step
    And It Is Created to the Same Suite Where Initial Step Is

Create New Keyword With Arguments
    Given Grid Contains Text
    When User Selects Create New UK from Context Menu on Line With Three values
    Then UK with Selected Text as Name Is Created
    And UK Have Two Arguments
    And History Back Points to the Initial Step
    And It Is Created to the Same Suite Where Initial Step Is

Create New Keyword With Arguments And Comments
    Given Grid Contains Text
    When User Selects Create New UK from Context Menu on Line With Three values And Comments
    Then UK with Selected Text as Name Is Created
    And UK Have Two Arguments
    And History Back Points to the Initial Step
    And It Is Created to the Same Suite Where Initial Step Is

*** Keywords ***
Given Grid Contains Text
    Select Tree Element  Create Keyword|Test Case

When User Selects Create New UK from Context Menu on Selection
    Set Test Variable  ${CREATED UK NAME}  Non Existing Keyword
    Click Editor Row And Column  1  1
    Create User Keyword

When User Selects Create New UK from Context Menu on Line With Three values
    Set Test Variable  ${CREATED UK NAME}  Non Existing Keyword With Args
    Click Editor Row And Column  2  1
    Create User Keyword

When User Selects Create New UK from Context Menu on Line With Three values And Comments
    Set Test Variable  ${CREATED UK NAME}  Non Existing Keyword With Args And Comments
    Click Editor Row And Column  3  1
    Create User Keyword

Then UK with Selected Text as Name Is Created
    Window Has Text  ${CREATED UK NAME}

And History Back Points to the Initial Step
    Sleep  0.1
    Go Back In History
    Window Has Text  Test Case

And It Is Created to the Same Suite Where Initial Step Is
    Select Tree Element  *Create Keyword|${CREATED UK NAME}
    Window Has Text  ${CREATED UK NAME}

And UK Have Two Arguments
    ${args} =  Get Arguments
    Should Be Equal  ${args}  \${arg1} | \${arg2}
